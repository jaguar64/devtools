/*
** 	BRIEF -- Basic Reconfigurable Interactive Editing Facility
**
** 	Written by Dave Nanian and Michael Strickman.
*/

/*
**		scrapper.cb:
**
**		This file saves the current scrap and scrap type to the state file
**	on exit.  To use it, put the string "-mscrapper" (no quotes) after the
**	"-mrestore" in your BFLAGS.  For example:
**
**		set bflags=-mrestore -mscrapper
*/

extern	_save_state (),
			_restore ();

replacement _save_state ()
{
	int	scrap_eoln,
			scrap_type;

	if (inq_scrap (scrap_eoln, scrap_type))
		{
		insert ("[brief-scrap]\n%d %d\n", scrap_eoln, scrap_type);
		paste ();
		insert ("[end-brief-scrap]\n");
		}
	returns (_save_state ());
}

replacement _restore ()
{
	_restore ();
	top_of_buffer ();

	if (search_fwd ("<\\[brief-scrap\\]>"))
		{
		int	scrap_eoln,
				scrap_type;

		string	line;

		down ();
		line = read ();
		scrap_eoln = atoi (line);
		scrap_type = atoi (substr (line, index (line, " ")));
		down ();
		drop_anchor (scrap_type);

		search_fwd ("\\[end-brief-scrap\\]>");

		if (scrap_type != 4)
			prev_char ();

		copy ();
		set_scrap_info (scrap_eoln);
		}
}
